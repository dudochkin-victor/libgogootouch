include(meegotouch_defines.prf)
include(meegotouch_mgen.prf)
include(meegotouch_mmoc.prf)

contains(MEEGOTOUCHCONFIG_DEPS, HAVE_DBUS) {
    QT *= dbus
}

macx {
    INCLUDEPATH += /Library/Frameworks/m.framework/Headers
}

contains(TEMPLATE, app) {
    DEFINES += M_APPLICATION_NAME=\\\"${QMAKE_TARGET}\\\"
} else {
    contains(TEMPLATE, lib) {
        DEFINES += M_LIBRARY_NAME=\\\"lib${QMAKE_TARGET}\\\"
    } else {
        # error(Unknown template)
    }
}

!contains(DEFINES, BUILD_M) {
    M_INC_DIR = $$M_PREFIX/include/meegotouch
    M_LIB_DIR = $$M_LIBDIR

    INCLUDEPATH += $${M_INC_DIR}
    DEPENDPATH += $${M_INC_DIR}

    LIBS += -L$${M_LIB_DIR}
}
