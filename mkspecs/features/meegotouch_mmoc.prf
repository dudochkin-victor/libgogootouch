# settings for meegotouch mmoc

isEmpty(_MEEGOTOUCH_MMOC_PRF_) {

    _MEEGOTOUCH_MMOC_PRF_ = 1

    contains(DEFINES, BUILD_M) {
        win32 {
            equals(QT_ARCH, arm) | equals(QT_ARCH, armv6) {
                M_MMOC_EXECUTABLE = perl.exe $$M_SOURCE_TREE/mmoc/mmoc.pl
            } else {
                debug {
                    M_MMOC_EXECUTABLE = $$M_BUILD_TREE/mmoc/debug/mmoc.exe
                } else {
                    M_MMOC_EXECUTABLE = $$M_BUILD_TREE/mmoc/release/mmoc.exe
	        }
            }
        } else {
            equals(QT_ARCH, arm) | equals(QT_ARCH, armv6) {
                M_MMOC_EXECUTABLE = perl $$M_SOURCE_TREE/mmoc/mmoc.pl
            } else {
                M_MMOC_EXECUTABLE = $$M_BUILD_TREE/mmoc/mmoc
            }
        }
    } else {
        win32 {
            M_MMOC_EXECUTABLE = perl.exe $$M_SOURCE_TREE/mmoc/mmoc.pl
        } else {
            equals(QT_ARCH, arm) | equals(QT_ARCH, armv6) {
                M_MMOC_EXECUTABLE = mmoc.pl
            } else {
                M_MMOC_EXECUTABLE = mmoc
            }
        }
    }

QMAKE_MOC = $$M_MMOC_EXECUTABLE

}
